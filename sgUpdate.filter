def ports:
   if .IpProtocol == "-1"
   then {FromPort:"ANY", ToPort:"ANY", IpProtocol:"ANY"}
   else {FromPort, ToPort, IpProtocol}
   end
;

def tabella($g; $p; $dir; $ip; $desc):
    [ $g.GroupId, $g.GroupName, $g.Description, $g.VpcId, $dir,
      $p.IpProtocol, $p.FromPort, $p.ToPort, $ip, $desc ]
;

def creatab:
      .SecurityGroups[]
    | { GroupId, GroupName, Description, VpcId } as $g
    | (
          .IpPermissions[]
        | ports as $p
        | ( .IpRanges[]         | tabella($g; $p; "IN"; .CidrIp; .Description) ),
          ( .UserIdGroupPairs[] | tabella($g; $p; "IN"; .GroupId; .Description) )
      ),
      (
          .IpPermissionsEgress[]
        | ports as $p
        | ( .IpRanges[]         | tabella($g; $p; "IN"; .CidrIp; .Description) ),
          ( .UserIdGroupPairs[] | tabella($g; $p; "IN"; .GroupId; .Description) )
      )
;

  creatab
| map(tostring)
| join("\t")